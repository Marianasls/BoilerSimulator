FUNCTION_BLOCK simulator  

    VAR_INPUT               // Definição das variáveis de entrada
        temperature: REAL;
        volume: REAL;
    END_VAR

    VAR_OUTPUT              // Definição das variáveis de saída
        pressure: REAL;
    END_VAR

    // definição das partições fuzzy e seus intervalos para cada variável de entrada
    FUZZIFY temperature
        TERM low := (800.0, 1) (900.0, 1) (1000.0, 0) ;
        TERM medium := (900.0, 0) (1000.0, 1) (1100.0, 0) ;
        TERM high := (1000.0, 0) (1100.0, 1) (1200.0, 1);
    END_FUZZIFY

    FUZZIFY volume
        TERM small := (2.0, 1) (4.5, 1) (7.0, 0) ;
        TERM medium := (4.5, 0) (7.0, 1) (9.5, 0) ;
        TERM large := (7.0, 0) (9.5, 1) (12.0, 1);
    END_FUZZIFY

    // definição das partições fuzzy e seus intervalos para cada variável de saída
    DEFUZZIFY pressure
        TERM low := (4.0, 1) (5.0, 1) (8.0, 0) ;
        TERM medium := (6.0, 0) (8.0, 1) (10.0, 0) ;
        TERM high := (8.0, 0) (11.0, 1) (12.0, 1) ;
        METHOD : COA;  // Método de defuzzificação (Centro de Área)
        DEFAULT := 0;  // Valor default caso nenhuma regra seja ativada
    END_DEFUZZIFY

    // Definição do conjunto de regras para o controlador Fuzzy.
    RULEBLOCK No1
        AND : MIN;  // Método MIN utilizado no processamento do operador lógico AND
        ACT : MIN;  // Método de ativação
        ACCU : MAX; // método de acumulação

        // Início da descrição de cada regra
        RULE 1 : IF temperature IS low AND volume IS small THEN pressure IS low;
        RULE 2 : IF temperature IS medium AND volume IS small THEN pressure IS low;
        RULE 3 : IF temperature IS high AND volume IS small THEN pressure IS medium;
        RULE 4 : IF temperature IS low AND volume IS medium THEN pressure IS low;
        RULE 5 : IF temperature IS medium AND volume IS medium THEN pressure IS medium;
        RULE 6 : IF temperature IS high AND volume IS medium THEN pressure IS high;
        RULE 7 : IF temperature IS low AND volume IS large THEN pressure IS medium;
        RULE 8 : IF temperature IS medium AND volume IS large THEN pressure IS high;
        RULE 8 : IF temperature IS high AND volume IS large THEN pressure IS high;

    END_RULEBLOCK

END_FUNCTION_BLOCK